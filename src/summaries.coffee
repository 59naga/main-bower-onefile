# Dependencies
bytes= require './bytes'

through2= require 'through2'
plugins= (require 'gulp-load-plugins')()

path= require 'path'

# Public
summaries= ({cwd,summary,outputBytes,outputName}={})->
  # Defaults
  cwd?= process.cwd()
  outputBytes?= no

  files= []
  if outputBytes
    console.log 'Found:'

  # Pulgin
  through2.obj (file,enc,next)->
    if outputBytes
      console.log '  ',bytes file,cwd

    files.push file
    next()

  ,(done)->
    if summary
      summary= """
      /**
       * Onefile:
       *   #{(bytes file,cwd for file in files).join('\n *   ')}
       * Total:
       *   #{bytes files.reduce ((total,file)-> total + file.contents.length),0} #{outputName}
       *
       * ... Generated by https://github.com/59naga/onefile
       **/


      """

      file= new plugins.util.File
        path: path.join cwd,'onefile-header'
        contents: new Buffer summary
      @push file

    @push file for file in files
    done()

module.exports= summaries
